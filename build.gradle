buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}
apply plugin: 'net.minecraftforge.gradle'

version = '7.9.1'
group = 'mod.lucky'
archivesBaseName = 'lucky'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: '20190719-1.14.3'
    
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'info'
            mods { lucky { source sourceSets.main } }
        }
        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'info'
            mods { lucky { source sourceSets.main } }
        }
    }
}

ext {
    forgeVersion = '1.14.4-28.1.116'
    mcversion = project.forgeVersion.split('-')[0]
    jarNameRaw = 'luckyblock-build'
    jarName = "luckyblock-${project.mcversion}-$version"
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.forgeVersion}"
}

apply from: 'scripts/tasks.gradle' // custom tasks

jar {
    archiveName = "${project.jarNameRaw}.jar"
    manifest {
        attributes([
            'Specification-Title': 'luckyblock',
            'Specification-Vendor': 'alexsocha',
            'Specification-Version': '1',
            'Implementation-Title': 'luckyblock',
            'Implementation-Version': version,
            'Implementation-Vendor': 'alexsocha',
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

processResources {
    eachFile {
        if (it.relativePath.segments[0] in ['META-INF']) {
            expand 'version':version
        }
    }
}
